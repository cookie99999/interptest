program -> declaration* EOF ;
declaration -> var-decl | statement ;
var-decl -> ("str" | "real" | "int") IDENTIFIER ("=" expression)? ";" ;
statement -> expr-stmt | print-stmt | block ;
block -> "begin" declaration* "end" ;
expr-stmt -> expression ";" ;
print-stmt -> "print" expression ";" ;
expression -> assignment ;
assignment -> IDENTIFIER "=" assignment | equality ;
equality -> comparison (("!=" | "==") comparison)* ;
comparison -> term ((">" | ">=" | "<" | "<=") term)* ;
term -> factor (("-" | "+") factor)* ;
factor -> unary (("/" | "*") unary)* ;
unary -> ("!" | "-") unary | primary ;
primary -> NUMBER | STRING | "true" | "false" | "nil" |
           "(" expression ")" | IDENTIFIER ;